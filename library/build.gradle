apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "0.0.1"

android {
  compileSdkVersion 26
  buildToolsVersion "26.0.3"

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 26
  }
}

dependencies {
  compile 'com.android.support:appcompat-v7:26.1.0'
  compile 'com.android.support:cardview-v7:26.1.0'
  compile 'com.android.support:recyclerview-v7:26.1.0'
  compile 'com.google.android.gms:play-services-ads:11.8.0'
  compileOnly 'com.squareup.picasso:picasso:2.5.2'
  compileOnly 'com.github.bumptech.glide:glide:3.7.0'
  compileOnly 'io.reactivex.rxjava2:rxandroid:2.0.2'
  compileOnly 'io.reactivex.rxjava2:rxjava:2.1.10'
}

def siteUrl = 'https://github.com/zac4j/Banner'
def gitUrl = 'https://github.com/zac4j/Banner.git'
group = "com.zac4j.library"

install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      artifactId = "Banner"
      project {
        packaging 'aar'

        // Add your description here
        name '<A image slider library in Android>'
        url siteUrl

        // Set your license
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id 'zac4j'
            name 'zac'
            email 'zac_ju@163.com'
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

artifacts {
  archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")

  configurations = ['archives']
  pkg {
    repo = "maven"
    name = "Banner"
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = ["Apache-2.0"]
    publish = true
  }
}